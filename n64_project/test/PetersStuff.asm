// N64 'Bare Metal' 16BPP 320x240 Fill Line RDP Demo by krom (Peter Lemon):
arch n64.cpu
endian msb
output "PetersStuff.N64", create
fill 1052672 // Set ROM Size

origin $00000000
base $80000000 // Entry Point Of Code

include "../LIB/N64.INC"
include "../LIB/N64_GFX.INC"
include "../LIB/COLORS16.INC"
include "N64_Header.asm"
insert "../LIB/N64_BOOTCODE.BIN"

Start:
	lui t0, PIF_BASE
	addi t1, 0, 8
	sw t1, PIF_CTRL(t0)


	// Video Initialization (320 x 240 x 16bit)

	lui t0, VI_BASE

	li t1, BPP16
	sw t1, VI_STATUS(t0)

	li t1, $A0100000
	sw t1, VI_ORIGIN(t0)

	li t1, 320
	sw t1, VI_WIDTH(t0)

	li t1, $200
	sw t1, VI_V_INTR(t0)

	li t1, 0
	sw t1, VI_V_CURRENT_LINE(t0)

	li t1, $3E52239
	sw t1, VI_TIMING(t0)

	li t1, $20D
	sw t1, VI_V_SYNC(t0)

	li t1, $C15
	sw t1, VI_H_SYNC(t0)

	li t1, $C150C15
	sw t1, VI_H_SYNC_LEAP(t0)

	li t1, $6C02EC
	sw t1, VI_H_VIDEO(t0)

	li t1, $2501FF
	sw t1, VI_V_VIDEO(t0)

	li t1, $E0204
	sw t1, VI_V_BURST(t0)

	li t1, ($100*((320)/160))
	sw t1, VI_X_SCALE(t0)

	li t1, ($100*((240)/60))
	sw t1, VI_Y_SCALE(t0)

  ScreenNTSC(320, 240, BPP16, $A0100000) // Screen NTSC: 320x240, 16BPP, DRAM Origin $A0100000

  WaitScanline($200) // Wait For Scanline To Reach Vertical Blank

  DPC(RDPBuffer, RDPBufferEnd) // Run DPC Command Buffer: Start, End

Loop:
  j Loop
  nop // Delay Slot

align(8) // Align 64-Bit
RDPBuffer:
arch n64.rdp
  Set_Scissor 0<<2,0<<2, 0,0, 320<<2,240<<2 // Set Scissor: XH 0.0,YH 0.0, Scissor Field Enable Off,Field Off, XL 320.0,YL 240.0
  Set_Other_Modes CYCLE_TYPE_FILL // Set Other Modes
  Set_Color_Image IMAGE_DATA_FORMAT_RGBA,SIZE_OF_PIXEL_16B,320-1, $00100000 // Set Color Image: FORMAT RGBA,SIZE 16B,WIDTH 320, DRAM ADDRESS $00100000
  Set_Fill_Color $FF01FF01 // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels
  Fill_Rectangle 319<<2,239<<2, 0<<2,0<<2 // Fill Rectangle: XL 319.0,YL 239.0, XH 0.0,YH 0.0

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $F001F001 // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (Red)
  // Line (Dir=1)
  //
  //          . v[1]:XL,XH(X:75.0) YM,YH(Y:50.0)
  //         / DxHDy
  //  DxLDy /
  //       . v[0]:(X:25.0) YL(Y:100.0)
  //
  // Output: Dir 1,Level 0,Tile 0, YL 100.0,YM 50.0,YH 50.0, XL 76.0,DxLDy -1.0, XH 75.0,DxHDy -1.0
     Fill_Triangle 1,0,0, 400,200,200, 76,0,-1,0, 75,0,-1,0, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $0F010F01 // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (Green)
  // Line (Dir=1)
  //
  //     . v[1]:XL,XH(X:100.0) YM,YH(Y:50.0)
  //      \ DxHDy
  // DxLDy \
  //        . v[0]:(X:150.0) YL(Y:100.0)
  //
  // Output: Dir 1,Level 0,Tile 0, YL 100.0,YM 50.0,YH 50.0, XL 101.0,DxLDy 1.0, XH 100.0,DxHDy 1.0
     Fill_Triangle 1,0,0, 400,200,200, 101,0,1,0, 100,0,1,0, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $003F003F // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (Blue)
  // Line (Dir=1)
  //
  //       . v[1]:XL,XH(X:175.0) YM,YH(Y:50.0)
  //       | DxHDy
  // DxLDy |
  //       . v[0]:(X:175.0) YL(Y:100.0)
  //
  // Output: Dir 1,Level 0,Tile 0, YL 100.0,YM 50.0,YH 50.0, XL 176.0,DxLDy 0.0, XH 175.0,DxHDy 0.0
     Fill_Triangle 1,0,0, 400,200,200, 176,0,0,0, 175,0,0,0, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $FFFFFFFF // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (White)
  // Line (Dir=1)
  //
  //  DxHDy
  //  .___. v[1]:XH(X:250.0) YL(Y:100.0), v[0]:XL(X:300.0) YM,YH(Y:100.0)
  //  DxLDy
  //
  // Output: Dir 1,Level 0,Tile 0, YL 101.0,YM 100.0,YH 100.0, XL 300.0,DxLDy 0.0, XH 250.0,DxHDy 0.0
     Fill_Triangle 1,0,0, 404,400,400, 300,0,0,0, 250,0,0,0, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $D001F001 // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (Red Strips)
  // Right Major Triangle (Dir=1)
  //
  //     . v[1]:XL,XH(X:25.0) YM,YH(Y:150.0)
  //      \ DxHDy
  // DxLDy \
  //        . v[0]:(X:75.0) YL(Y:175.0)           
  //
  // Output: Dir 1,Level 0,Tile 0, YL 175.0,YM 150.0,YH 150.0, XL 27.0,DxLDy 2.0, XH 25.0,DxHDy 2.0
     Fill_Triangle 1,0,0, 700,600,600, 27,0,2,0, 25,0,2,0, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $0D010F01 // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (Green Strips)
  // Line (Dir=1)
  //
  //         . v[1]:XL,XH(X:150.0) YM,YH(Y:150.0)
  //        /
  // DxLDy / DxHDy
  //      /
  //     . v[0]:(X:125.0) YL(Y:200.0)
  //
  // Output: Dir 1,Level 0,Tile 0, YL 200.0,YM 150.0,YH 150.0, XL 151.0,DxLDy -0.5, XH 150.0,DxHDy -0.5
     Fill_Triangle 1,0,0, 800,600,600, 151,0,-1,32768, 150,0,-1,32768, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $001F003F // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (Blue Strips)
  // Line (Dir=1)
  //
  //         . v[1]:XH,XM(X:225.0) YH(Y:150.0)
  //        / DxHDy
  // DxLDy /
  //      . v[0]:XL(X:175.0) YM(Y:175.0)
  //
  // Output: Dir 1,Level 0,Tile 0, YL 175.0,YM 150.0,YH 150.0, XL 227.0,DxLDy -2.0, XH 225.0,DxHDy -2.0
     Fill_Triangle 1,0,0, 700,600,600, 227,0,-2,0, 225,0,-2,0, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Pipe // Stall Pipeline, Until Preceeding Primitives Completely Finish
  Set_Fill_Color $DDDDFFFF // Set Fill Color: PACKED COLOR 16B R5G5B5A1 Pixels (White Strips)
  // Line (Dir=1)
  //
  //     . v[1]:XH,XM(X:275.0) YH(Y:150.0)
  //      \
  // DxLDy \ DxHDy
  //        \  
  //         . v[0]:(X:300.0) YL(Y:200.0)
  //
  // Output: Dir 1,Level 0,Tile 0, YL 200.0,YM 150.0,YH 150.0, XL 276.0,DxLDy -0.5, XH 275.0,DxHDy -0.5
     Fill_Triangle 1,0,0, 800,600,600, 276,0,0,32768, 275,0,0,32768, 0,0,0,0 // Generated By N64LineCalc.py

  Sync_Full // Ensure Entire Scene Is Fully Drawn
RDPBufferEnd: